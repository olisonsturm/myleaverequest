@Metadata.layer: #CORE
@UI.headerInfo: {
  typeNamePlural: 'Employees',
  title.value: 'FullName',
  description.value: 'EmployeeNumber'
}
@UI.presentationVariant: [{sortOrder: [{ by: 'EmployeeNumber', direction: #ASC }]}]
annotate entity ZC_CEO_Employee with
{
  /* Facets */
  @UI.facet:
  [
    { position: 10, type: #FIELDGROUP_REFERENCE, label: 'Details', targetQualifier: 'details' },
    { position: 20, type: #LINEITEM_REFERENCE,  targetElement: '_LeaveEntitlements', label: 'Entitlements' },
    { position: 30, type: #LINEITEM_REFERENCE,  targetElement: '_LeaveRequests', label: 'Leave Requests' },
    { position: 40, type: #FIELDGROUP_REFERENCE, label: 'Administrative Data', targetQualifier: 'admindata' }
  ]

  /* Aktions */
  @UI.identification: [
    { position: 10, type: #FOR_ACTION, dataAction: 'Refresh', label: 'Refresh' }
  ]

  /* Fields */
  @UI.hidden: true
  EmployeeId;

  @EndUserText: { label: 'Employee Number', quickInfo: 'Number of Employee'}
  @UI.selectionField: [{ position: 10 }]
  @UI.lineItem: [{ position: 10 }]
  @UI.fieldGroup: [{ position: 10, qualifier: 'details' }]
  EmployeeNumber;

  @EndUserText: { label: 'First Name', quickInfo: 'Givenname'}
  @UI.lineItem: [{ position: 20 }]
  @UI.fieldGroup: [{ position: 20, qualifier: 'details' }]
  FirstName;

  @EndUserText: { label: 'Last Name', quickInfo: 'Surname'}
  @UI.lineItem: [{ position: 30 }]
  @UI.fieldGroup: [{ position: 30, qualifier: 'details' }]
  LastName;

  @UI.hidden: true
  FullName;

  @EndUserText: { label: 'Entry Date', quickInfo: 'Entry Date'}
  @UI.selectionField: [{ position: 20 }]
  @UI.lineItem: [{ position: 40 }]
  @UI.fieldGroup: [{ position: 50, qualifier: 'details' }]
  EntryDate;

  @EndUserText: { label: 'Available Vacation Days', quickInfo: 'Availavle Vacation Days'}
  @UI.lineItem: [{ position: 50, criticality: 'AvailableVDaysCriticality', criticalityRepresentation: #WITHOUT_ICON }]
  @UI.dataPoint: { description: 'Available Vacation Days', title: 'Available Vacation Days', targetValueElement: 'AvailableVacationDays', criticality: 'AvailableVDaysCriticality' }
  AvailableVacationDays;
  @EndUserText: { label: 'Consumed Vacation Days', quickInfo: 'Consumed Vacation Days'}
  @UI.lineItem: [{ position: 60, criticality: 'ConsumedVDaysCriticality', criticalityRepresentation: #WITHOUT_ICON }]
  @UI.dataPoint: { description: 'Consumed Vacation Days', title: 'Consumed Vacation Days', targetValueElement: 'ConsumedVacationDays', criticality: 'ConsumedVDaysCriticality'}
  ConsumedVacationDays;
  @EndUserText: { label: 'Planned Vacation Days', quickInfo: 'Planned Vacation Days'}
  @UI.lineItem: [{ position: 70, criticality: 'PlannedVDaysCriticality', criticalityRepresentation: #WITHOUT_ICON }]
  @UI.dataPoint: { description: 'Available Vacation Days', title: 'Planned Vacation Days', targetValueElement: 'PlannedVacationDays', criticality: 'PlannedVDaysCriticality'}
  PlannedVacationDays;

  @UI.hidden: true
  AvailableVDaysCriticality;

  @UI.hidden: true
  ConsumedVDaysCriticality;

  @UI.hidden: true
  PlannedVDaysCriticality;

  /* Admin Data */
  @UI.fieldGroup: [{ position: 10, qualifier: 'admindata' }]
  CreatedBy;
  @UI.fieldGroup: [{ position: 20, qualifier: 'admindata' }]
  CreatedAt;
  @UI.fieldGroup: [{ position: 30, qualifier: 'admindata' }]
  LastChangedBy;
  @UI.fieldGroup: [{ position: 40, qualifier: 'admindata' }]
  LastChangedAt;
}